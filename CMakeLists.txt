cmake_minimum_required(VERSION 3.16)

# Pre-generated portal asset .S files
# These files are generated by running ./build_assets.sh
# and are committed to the repository to support PlatformIO builds.
set(PORTAL_EMBED_SOURCES
    ${CMAKE_CURRENT_LIST_DIR}/portal_assets/index.html.S
    ${CMAKE_CURRENT_LIST_DIR}/portal_assets/app.js.S
    ${CMAKE_CURRENT_LIST_DIR}/portal_assets/index.css.S
)

idf_component_register(
    SRCS
        "src/gateway.cpp"
        "src/handlers/device_handler.cpp"
        "src/handlers/health_handler.cpp"
        "src/handlers/log_handler.cpp"
        "src/handlers/metrics_handler.cpp"
        "src/handlers/mdns_handler.cpp"
        "src/handlers/portal_handler.cpp"
        "src/handlers/wifi_handler.cpp"
        "src/http_server.cpp"
        "src/logging.cpp"
        "src/mdns_service.cpp"
        "src/middlewares/logging.cpp"
        "src/validation.cpp"
        "src/wifi_service.cpp"
        "src/wifi_credentials.cpp"
        ${PORTAL_EMBED_SOURCES}
    INCLUDE_DIRS
        "include"
    PRIV_INCLUDE_DIRS
        "src"
    REQUIRES
        esp_http_server
        json
        esp_wifi
    PRIV_REQUIRES
        esp_netif
        esp_event
        nvs_flash
        log
        mdns
        esp_timer
        heap
)

target_compile_features(${COMPONENT_LIB} PUBLIC cxx_std_20)

# Add version from idf_component.yml as compile definition
file(READ "${CMAKE_CURRENT_LIST_DIR}/idf_component.yml" IDF_COMPONENT_YML)
string(REGEX MATCH "version: *([0-9]+\\.[0-9]+\\.[0-9]+)" _ "${IDF_COMPONENT_YML}")
set(GATEWAY_VERSION "${CMAKE_MATCH_1}")
target_compile_definitions(${COMPONENT_LIB} PUBLIC GATEWAY_VERSION="${GATEWAY_VERSION}")
message(STATUS "Set GATEWAY_VERSION to: ${GATEWAY_VERSION}")
